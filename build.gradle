plugins {
    id 'java-library'
    id 'io.freefair.lombok' version '8.11'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.14.0'
}

group 'io.github.ya-b'
version '0.1.5'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}


repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'com.google.cloud.tools:jib-core:0.27.3'

    testImplementation 'ch.qos.logback:logback-classic:1.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test', Test) {
    useJUnitPlatform()

    maxHeapSize = '1G'

    testLogging {
        events "passed"
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.encoding = "UTF-8"
    failOnError = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'registry-client'
                description = 'A java client for docker registry api v2'
                url = 'https://github.com/ya-b/registry-client.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ya-b'
                        name = 'yangbo'
                        email = 'ya-b@outlook.com'
                    }
                }
                scm {
                    url = 'https://github.com/ya-b/registry-client.git'
                    connection = 'scm:git:https://github.com/ya-b/registry-client.git'
                    developerConnection = 'scm:git:https://github.com/ya-b/registry-client.git'
                }
            }

        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}

